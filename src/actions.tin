#CLASS {actions} {kill};
#CLASS {actions} {open};

#NOP ==[HEARTSTONE]==
#ACTION {%1 notices a heartstone} {
    gs HEARTSTONE!!! HEARTSTONE!!!;
    #math hscount (${hscount} + 1);
    #math avghscount ((${totalxpcount}/${hscount}*10)*0.1);
    gs Heartstone counter: ${hscount}, avg mobs per hs: ${avghscount};
}
#ACTION {You see the glint of a heartstone!!} {
    gs I POPED A HEARTSTONE!!!;
    #math hscount (${hscount} + 1);
    #math avghscount ((${totalxpcount}/${hscount}*10)*0.1);
    gs Heartstone counter: ${hscount}, avg mobs per hs: %{avghscount};

    #ACTION {(Terrible) : %1 Heartstone} {#var lasths %1};
    inventory;
    gs The hearstone I just poped is ${lasths} one!;
    #DELAY 5 {#UNACTION {(Terrible) : %1 Heartstone}};
}
#HIGHLIGHT {You see the glint of a heartstone!!} {bold red underscore}
#HIGHLIGHT {%1 notices a heartstone%*} {bold red underscore}

#NOP ==[Auto Assist]==
#ACTION {{The \w+ (.*?) \w.* (blast|crush|hit|pierce|pound|slash|whip).*}} {
    #if {"%2" == "${assist}" && "${assist_fired}" == "0"} {assist; #var assist_fired 1;};
} {4};
#ACTION {{(.*?) \w.* (blast|crush|hit|pierce|pound|slash|whip).*}} {
    #if {"%2" == "${assist}" && "${assist_fired}" == "0"} {assist; #var assist_fired 1;};
} {5};

#NOP ==[Stat Tracker]==
#ACTION {You receive your share of experience -- %1 points.} {
    #IF {"${AUTO_LOOT}" == "1"} {get coin all.corp;};
    #math groupxp (${groupxp} + %1);
    #math totalxp (${totalxp} + %1);
    #math groupxpcount (${groupxpcount} + 1);
    #math totalxpcount (${totalxpcount} + 1);
    #var assist_fired 0;
}
#ACTION {You receive your share of experience -- 1 measly little point.} {
    #IF {"${AUTO_LOOT}" == "1"} {get coin all.corp;};
    #math groupxp (${groupxp} + 1);
    #math totalxp (${totalxp} + 1);
    #math groupxpcount (${groupxpcount} + 1);
    #math totalxpcount (${totalxpcount} + 1);
    #var assist_fired 0;
}
#ACTION {You receive %1 experience points.} {
    #IF {"${AUTO_LOOT}" == "1"} {get coin all.corp;};
    #math soloxp (${soloxp} + %1);
    #math totalxp (${totalxp} + %1);
    #math soloxpcount (${soloxpcount} + 1);
    #math totalxpcount (${totalxpcount} + 1);
    #var assist_fired 0;
}

#NOP ==[Group Helper]==
#ACTION {You are now a member of %*'s group.} {
    #FORMAT {assist} {@get_real_name{%1}};
    #SHOWME {\e[91m===== CURRENT ASSITING: $assist =====};
    #if {"${assist}" != "Shuu" && "${CURRENT_CHAR}" == "Shuu"} {
        gs gs 'shuu trigs' to check my trigs;
    };
    #else {#var assist shuu};
};
#ACTION {%* beckons for you to follow.} {
    #FORMAT {tmp} {@get_real_name{%1}};
    #IF {"${tmp}" == "{Istvani|Aelloir|Lyzivex|Azil}"} {follow $tmp;};
};
#ACTION {There were %1 coins.} {split %1}

#NOP ==[End Session On Quit]==
#ACTION {%1 stores your belongings and helps you into your private chamber.} {
    #CLASS variables open;
    #CLASS variables write {src/variables.tin};
    #end;
}

#NOP ==[Auction Checker]==
#ACTION {[Auto-Auc] : %1 auctioning %2, min is %3.} {bid ?}

#NOP ==[Misc]==
#ACTION {You have been idle, and are pulled into a void.} {rub}
#ACTION {You stop following %*} {group me}

#NOP ==[Loot Alert]==
#HIGHLIGHT {%*essence crystallizes into a soul-riven shard!} {b red}
#HIGHLIGHT {You discover the Cipher of %1 on the corpse of %2!} {b red}

#NOP ==[World Highlighter]==
#HIGHLIGHT {A gold coin is floating here.} {\e[36m}
#HIGHLIGHT {An air bubble%*} {b red}
#HIGHLIGHT {A small pile of gem shards sits here%*} {\e[91m}
#HIGHLIGHT {One miserable gold sovereign is lying here.} {\e[91m}
#HIGHLIGHT {You manage to %* the last of the %*} {\e[91m}
#HIGHLIGHT {You spot something shiny!  Dig more!} {b red}
#HIGHLIGHT {%*you manage to salvage part of %* body!} {b red}
#HIGHLIGHT {The corpse of the Spiralborn decays in your hands.} {b red}

#NOP ==[Game Alert]==
#HIGHLIGHT {A great sandstorm rages across the desert, carrying a hazy city with it.} {b red}
#HIGHLIGHT {A chaodyn rises up, apparently resurrected from his death.} {b red}
#HIGHLIGHT {As the Spiralborn dies, %*.} {b red}
#HIGHLIGHT {Namegi the Betrayer's shield of fire flairs up to full strength!} {b red}
#HIGHLIGHT {You feel an evil mist surround you.} {b red}
#HIGHLIGHT {Vzikronys, The Worm-Who-Walks screeches and spills out onto the floor as a mass of vermin!} {b red}
#HIGHLIGHT {You feel the world fall out beneath you, %* has summoned you!} {b red}
#HIGHLIGHT {You wish that your wounds would stop BLEEDING so much!} {b red}
#ACTION {You drive %1 forward in exhaustion!} {gs mount is dead!}
#ACTION {You feel like you can fight with some effectiveness now..} {stand}
#ACTION {You wish that your wounds would stop BLEEDING so much!} {#IF {"${CURRENT_CHAR}" == "Shuu"} {recall};}

#NOP ==[Buff Alert]==
#ACTION {The affects of the Bard's song dissipate.} {
    gs Bard Song OUT!;
    _update_noti_ \e[91m===Bard Song OUT!===\e[0m;
};
#ACTION {The tower of mortal will around you fades.} {
    gs TOWER OUT!;
    _update_noti_ \e[91m===TOWER OUT!===\e[0m;
};
#ACTION {The white aura around your body fades.} {
    gs SANC OUT!;
    _update_buff_status_ sanc off;
    #IF {"${CURRENT_CHAR}" == "Shuu"} {weave 'sanc' me};
    _update_noti_ \e[91m===SANC OUT!===\e[0m;
};
#ACTION {You feel an evil mist surround you.} {
    gs DESSED!
    _update_noti_ \e[91m===DESSED!!!===\e[0m;
};
#ACTION {You feel yourself exposed.} {
    gs II OUT!;
    _update_buff_status_ ii off;
    #IF {"${CURRENT_CHAR}" == "Shuu"} {weave 'improved invis' me};
    _update_noti_ \e[91m===II OUT!===\e[0m;
};
#ACTION {You lose your focus.} {
    gs FOCUS OUT!;
    _update_buff_status_ focus off;
    _update_noti_ \e[91m===FOCUS OUT!===\e[0m;
};
#ACTION {You no longer feel in tune with nature.} {
    gs NATFURY OUT!;
    _update_buff_status_ nf off;
    #IF {"${CURRENT_CHAR}" == "Shuu"} {weave 'nature fury'};
    _update_noti_ \e[91m===NATFURY OUT!===\e[0m;
};
#ACTION {Your body and mind return to their normal speed.} {
    gs HASTE OUT!;
    _update_buff_status_ haste off;
    #IF {"${CURRENT_CHAR}" == "Shuu"} {weave 'haste'};
    _update_noti_ \e[91m===HASTE OUT!===\e[0m;
};
#ACTION {Your body stops absorbing light%*} {
    gs VS OUT!;
    _update_noti_ \e[91m===VS OUT!===\e[0m;
};
#ACTION {Your cloak of darkness dissolves.} {
    gs COD OUT!;
    #IF {"${CURRENT_CHAR}" == "Aldrich"} {weave 'cloak of darkness'};
    _update_noti_ \e[91m===COD OUT!===\e[0m;
};
#ACTION {You feel calm and your lust for blood lessens.} {
    _update_noti_ \e[36m===Blood Lust OUT!===;
};
#ACTION {You feel less protected.} {
    _update_buff_status_ armor off;
    _update_noti_ \e[36m===Armor Out!===\e[0m;
};
#ACTION {You feel less righteous.} {
    _update_buff_status_ bless off;
    #IF {"${CURRENT_CHAR}" == "Shuu"} {weave 'bless' me};
    _update_noti_ \e[36m===Bless Out!===\e[0m;
};
#ACTION {You feel the aiding deity reclaim its donated strength.} {
    _update_buff_status_ aid off;
    #IF {"${CURRENT_CHAR}" == "{Shuu|Aldrich}"} {weave 'aid' me};
    _update_noti_ \e[36m===Aid Out!===\e[0m;
};
#ACTION {You feel vulnerable to the elements again.} {
    _update_buff_status_ endure off;
    #IF {"${CURRENT_CHAR}" == "{Shuu|Ilia}"} {weave 'endure' me};
    _update_noti_ \e[36m===Endure Out!===\e[0m;
};
#ACTION {Your body loses its fiery glow.} {
    _update_noti_ \e[36m===FShield OUT!===\e[0m;
};
#ACTION {You stop your war-like chanting.} {
    #IF {"${CURRENT_CHAR}" == "Aldrich"} {weave 'war chant'};
    _update_noti_ \e[91m===WarChant OUT!===\e[0m;
};
#ACTION {Your mantle of chaos explodes in a burst of light!} {
    _update_noti_ \e[36m===Mantle Out!===\e[0m;
};
#ACTION {Your skin doesn't feel quite so tough anymore.} {
    _update_noti_ \e[36m===BarkSkin Out!===\e[0m;
};

#ACTION {You feel strengthened by the power of the Creator!} {
    _update_buff_status_ aid on;
};
#ACTION {You start glowing.} {
    _update_buff_status_ sanc on;
};
#ACTION {You feel fast!} {
    _update_buff_status_ haste on;
};
#ACTION {You vanish.} {
    _update_buff_status_ ii on;
};
#ACTION {You become the ultimate embodiment of the destructive power of nature!} {
    _update_buff_status_ nf on;
};
#ACTION {You feel as if you can breathe under water!} {
    _update_buff_status_ bw on;
};
#ACTION {You begin to focus your energies into yourself.} {
    _update_buff_status_ focus on;
};
#ACTION {You feel someone protecting you.} {
    _update_buff_status_ armor on;
};
#ACTION {You feel righteous.} {
    _update_buff_status_ bless on;
};
#ACTION {You feel a shield of energy surround your body.} {
    _update_buff_status_ endure on;
};

#NOP ==[Game Helper]==
#ACTION {You are knocked from your feet and fall hard to the ground!} {stand}
#ACTION {You are knocked from your mount and fall hard to the ground!} {stand}
#ACTION {You awaken from your deep sleep.} {
    #FORMAT {tmp} {%t} {%s};
}

#NOP ==[Scoreaff]==
#ACTION {You are invisible (improved).} {
    _update_buff_status_ ii on;
};
#ACTION {You are protected by Sanctuary.} {
    _update_buff_status_ sanc on;
};
#ACTION {You are fast!} {
    _update_buff_status_ haste on;
};
#ACTION {You are protected from elements.} {
    _update_buff_status_ endure on;
};
#ACTION {You are focused!} {
    _update_buff_status_ focus on;
};

#CLASS {actions} {close};
